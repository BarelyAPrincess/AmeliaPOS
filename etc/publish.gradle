// child script to publish to a maven repo
apply plugin: 'maven-publish'

// allow javadoc archive on Java 8 even if the javadoc isn't complete
if ( JavaVersion.current().isJava8Compatible() )
{
	allprojects {
		tasks.withType( Javadoc ) {
			options.addStringOption( 'Xdoclint:none', '-quiet' )
		}
	}
}

// source jar
task sourcesJar( type: Jar ) {
	classifier = 'sources'
	from sourceSets.main.java.srcDirs
}

// javadoc jar
task javadocJar( type: Jar, dependsOn: javadoc ) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

publishing {
	publications {
		maven( MavenPublication ) {
			pom.withXml {
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name project.name
					description description
					url 'https://github.com/BarelyAPrincess/AmeliaPOS'
					scm {
						url 'https://github.com/BarelyAPrincess/AmeliaPOS'
						connection 'scm:git:ssh:git@github.com:BarelyAPrincess/AmeliaPOS.git'
						developerConnection 'scm:git:ssh:git@github.com:BarelyAPrincess/AmeliaPOS.git'
					}
					licenses {
						license {
							name 'MIT License'
							url 'https://opensource.org/licenses/MIT'
							distribution 'repo'
						}
					}
					developers {
						developer {
							id 'BarelyAPrincess'
							name 'Amelia Sara (Mille)'
							email 'me@missameliasara.com'
						}
					}
				}
			}

			artifact jar
			artifact sourcesJar
			artifact javadocJar

			repositories {
				maven {
					// credentials
					credentials {
						username System.getenv( "publishUser" )
						password System.getenv( "publishPass" )
					}
					// url depends on version
					if ( System.getenv( "releaseBuild" ) != null )
					{
						url = 'http://jenkins.penoaks.com/artifactory/releases/'
					}
					else
					{
						url = 'http://jenkins.penoaks.com/artifactory/snapshots/'
					}
				}
			}
		}
	}
}
